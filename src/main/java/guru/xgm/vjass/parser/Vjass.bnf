{
    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Vjass"
    psiImplClassSuffix="Impl"

    parserClass=              "guru.xgm.vjass.parser.VjassParser"
    psiPackage=               "guru.xgm.vjass.psi"
    psiImplPackage=           "guru.xgm.vjass.psi.impl"
    elementTypeHolderClass=   "guru.xgm.vjass.psi.VjassTypes"
    elementTypeClass=         "guru.xgm.vjass.psi.tree.VjassIElement"
    tokenTypeClass=           "guru.xgm.vjass.psi.tree.VjassIToken"

    extends(".*Expr")=Expr

    tokens = [
        WHITE_SPACE='regexp:\s+'
        SINGLE_LINE_COMMENT="regexp://[^\n]*"
        BLOCK_COMMENT='regexp:/\*(.|\n)*?\*/'

        AND='and'
        ARRAY='array'
        BOOLEAN='boolean'
        CODE='code'
        CALL='call'
        CONSTANT='constant'
        DEBUG='debug'
        ELSE='else'
        ELSEIF='elseif'
        ENDFUNCTION='endfunction'
        ENDIF='endif'
        ENDLOOP='endloop'
        ENDGLOBALS='endglobals'
        ENDMODULE='endmodule'
        ENDSTRUCT='endstruct'
        ENDLIBRARY='endlibrary'
        EXTENDS='extends'
        ENDMETHOD='endmethod'
        EXTENDS='extends'
        EXITWHEN='exitwhen'
        FALSE='false'
        FUNCTION='function'
        GLOBALS='globals'
        HANDLE='handle'
        HOOK='hook'
        IF='if'
        IMPLEMENT='implement'
        INTEGER='integer'
        LIBRARY='library'
        LOCAL='local'
        LOOP='loop'
        METHOD='method'
        MODULE='module'
        NATIVE='native'
        NOT='not'
        NOTHING='nothing'
        NULL='null'
        OR='or'
        OPTIONAL='optional'
        PRIVATE='private'
        PUBLIC='public'
        REAL='real'
        RETURNS='returns'
        RETURN='return'
        REQUIRES='requires'
        STATIC='static'
        SET='set'
        STRING='string'
        STRUCT='struct'
        TAKES='takes'
        THEN='then'
        TRUE='true'
        TYPE='type'

        DOT='.'
        COMMA=','
        EQ_EQ='=='
        EQ='='
        NEQ='!='
        LT_EQ='<='
        LT='<'
        GT_EQ='>='
        GT='>'
        PLUS='+'
        MINUS='-'
        MUL='*'
        DIV='/'
        LPAREN='('
        RPAREN=')'
        LBRACK='['
        RBRACK=']'

        REALVAl='regexp:\d+\.\d*|\.\d+'
        HEXVAL='regexp:(0x|\$)[0-9a-fA-F]+'
        INTVAL='regexp:\d+'
        RAWVAl="regexp:'[^']*'"
        STRVAL="regexp:\"([^\"\\]|\\.)*\""

        ID='regexp:[A-Za-z_][_0-9A-Za-z]*'
    ]
}

Root ::= (
    TypeDef |
    NativeDef |
    GlobalsDef |
    FuncDef |
    LibDef |
    HookDef |
    StructDef |
    ModuleDef
    )* {recoverWhile=rootRecover}
private rootRecover ::= !(TYPE|CONSTANT|GLOBALS|NATIVE|FUNCTION)

// === VISIBILITY
Visibility ::= (PUBLIC|PRIVATE)

// === LIBRARY
LibDef ::= LIBRARY ID LibReq? (StructDef|ModuleDef|FuncDef|GlobalsDef|HookDef)* ENDLIBRARY {pin=1}
LibReq ::= REQUIRES LibReqItem (COMMA LibReqItem)* {pin=1}
LibReqItem ::= OPTIONAL? ID {pin=2}

// === TYPE
TypeDef ::= TYPE TypeName EXTENDS TypeNameBase {pin=1}
TypeName ::= ID|HANDLE|INTEGER|REAL|BOOLEAN|STRING|CODE|KEY
TypeNameBase ::= TypeName

// === VARIABLE
TypedVar ::= TypeName ID
TypedVarList ::= TypedVar (COMMA TypedVar)* {pin=1 recoverWhile=TypedVarListRecover}
private TypedVarListRecover ::= !(RETURNS)

// === FUNCTION
Arg ::= Expr|FuncAsCode
ArgList ::= Arg (COMMA Arg)*
FuncCall ::= FuncCallName LPAREN ArgList? RPAREN {pin=2}
FuncCallName ::= ID
FuncDefName ::= ID
FuncTakes ::= TAKES (NOTHING|TypedVarList)
FuncReturns ::= RETURNS (NOTHING|TypeName)
NativeDef ::= CONSTANT? NATIVE FuncDefName FuncTakes? FuncReturns? {pin=2}
FuncDef ::= Visibility? CONSTANT? FUNCTION FuncDefName FuncTakes? FuncReturns? Stmt* ENDFUNCTION {pin=3}

// === HOOK
HookDef ::= HOOK ID ID {pin=1}

// === GLOBALS
GlobalsDef ::= GLOBALS GvarDef* ENDGLOBALS {pin=1}
GvarName ::= ID
GvarDef ::= Visibility? CONSTANT? TypeName ARRAY? GvarName (EQ Expr)? {pin=3 recoverWhile=GvarDefRecover}
private GvarDefRecover ::= !(CONSTANT|GLOBALS|ENDGLOBALS|TypeName|Visibility)

// === STRUCT
StructDef ::= Visibility? STRUCT StructName (EXTENDS (ID|ARRAY))? (MethodDef|ImplementDef)* ENDSTRUCT {pin=2}
StructName ::= ID
MethodDef ::= Visibility? STATIC? METHOD ID FuncTakes? FuncReturns? Stmt* ENDMETHOD {pin=3}
DotTail ::= DOT (FuncCall|ArrayAccess|ID) {pin = 1}
ImplementDef ::= IMPLEMENT ID

// === MODULE
ModuleDef ::= Visibility? MODULE ID MethodDef* ENDMODULE {pin=2}

// === STATEMENT
Stmt ::=
    CallStmt |
    SetStmt |
    LocalVarStmt |
    ReturnStmt |
    IfStmt |
    LoopStmt |
    ExitWhenStmt
    {recoverWhile=StmtRecover}

private StmtRecover ::= !(CALL|DEBUG|ELSE|ELSEIF|ENDFUNCTION|ENDLOOP|ENDIF|EXITWHEN|FUNCTION|IF|LOCAL|LOOP|RETURN|SET|STATIC|ENDMETHOD|ID EQ|ID LPAREN|ID LBRACK|ID DOT|TypeName|Visibility)

LocalVarStmt ::= LOCAL? TypeName ARRAY? ID (EQ Expr)? {pin=2}

SetStmt ::= SET? (ArrayAccess|ID) DotTail* EQ Expr {pin=4}

CallStmt ::= DEBUG? CALL? (FuncCall DotTail*|ID DotTail+)

ReturnStmt ::= RETURN Expr? {pin=1}

IfStmt ::= STATIC? IF Expr THEN? (Stmt|ElseIfStmt|ElseStmt)* ENDIF {pin=2}
ElseIfStmt ::= ELSEIF Expr THEN? Stmt* {pin=1}
ElseStmt ::= ELSE Stmt* {pin=1}

LoopStmt ::= LOOP Stmt* ENDLOOP {pin=1}
ExitWhenStmt ::= EXITWHEN Expr {pin=1}

// === EXPRESSION
// https://github.com/JetBrains/Grammar-Kit/blob/master/TUTORIAL.md
// https://github.com/JetBrains/Grammar-Kit/blob/master/testData/generator/ExprParser.bnf
// https://intellij-support.jetbrains.com/hc/en-us/community/posts/115000094104-Expression-handling-in-grammar-kit-issue-with-deep-trees
// https://gist.github.com/FatalCatharsis/aad0b72c3ffed144c3d61ee73dcb283f

Expr ::=
    AddGroup |
    MulGroup |
    UnaryGroup |
    CmpGroup |
    OrExpr |
    AndExpr |
    PrimaryExpr

private AddGroup ::= PlusExpr | MinusExpr
PlusExpr ::= Expr PLUS Expr
MinusExpr ::= Expr MINUS Expr

private MulGroup ::= MulExpr | DivExpr
MulExpr ::= Expr MUL Expr
DivExpr ::= Expr DIV Expr

private UnaryGroup ::=
    MulUnaryExpr |
    DivUnaryExpr |
    PlusUnaryExpr |
    MinusUnaryExpr |
    NotExpr

MulUnaryExpr ::= MUL Expr
DivUnaryExpr ::= DIV Expr
PlusUnaryExpr ::= PLUS Expr
MinusUnaryExpr ::= MINUS Expr
NotExpr ::= NOT Expr

private CmpGroup ::=
    EqExpr |
    NeqExpr |
    LtExpr |
    LtEqExpr |
    GtExpr |
    GtEqExpr

EqExpr ::= Expr EQ_EQ Expr
NeqExpr ::= Expr NEQ Expr
LtExpr ::= Expr LT Expr
LtEqExpr ::= Expr LT_EQ Expr
GtExpr ::= Expr GT Expr
GtEqExpr ::= Expr GT_EQ Expr

OrExpr ::= Expr OR Expr
AndExpr ::= Expr AND Expr
ParenExpr ::= LPAREN Expr RPAREN

PrimaryExpr ::=
    FALSE |
    NULL |
    TRUE |
    ParenExpr |
    ArrayAccess |
    FuncCall |
    FuncAsCode |
    REALVAL |
    HEXVAL |
    INTVAL |
    RAWVAL |
    STRVAL |
    ID

ArrayAccess ::= ID LBRACK Expr? RBRACK
FuncAsCode ::= FUNCTION FuncCallName {pin=1}
