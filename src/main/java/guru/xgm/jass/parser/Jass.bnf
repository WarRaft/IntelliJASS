{
    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Jass"
    psiImplClassSuffix="Impl"

    parserClass=              "guru.xgm.jass.parser.JassParser"
    psiPackage=               "guru.xgm.jass.psi"
    psiImplPackage=           "guru.xgm.jass.psi.impl"
    elementTypeHolderClass=   "guru.xgm.jass.psi.JassTypes"
    elementTypeClass=         "guru.xgm.jass.psi.tree.JassIElement"
    tokenTypeClass=           "guru.xgm.jass.psi.tree.JassIToken"

    generateTokenAccessors=true
    generateTokens=true

    extends(".*Expr")=Expr

    tokens = [
        WHITE_SPACE='regexp:\s+'
        SINGLE_LINE_COMMENT="regexp://[^\n]*"

        AND='and'
        ARRAY='array'
        BOOLEAN='boolean'
        CODE='code'
        CALL='call'
        CONSTANT='constant'
        DEBUG='debug'
        ELSE='else'
        ELSEIF='elseif'
        ENDFUNCTION='endfunction'
        ENDIF='endif'
        ENDLOOP='endloop'
        ENDGLOBALS='endglobals'
        EXTENDS='extends'
        EXITWHEN='exitwhen'
        FALSE='false'
        FUNCTION='function'
        GLOBALS='globals'
        HANDLE='handle'
        IF='if'
        INTEGER='integer'
        LOCAL='local'
        LOOP='loop'
        NATIVE='native'
        NOT='not'
        NOTHING='nothing'
        NULL='null'
        OR='or'
        REAL='real'
        RETURNS='returns'
        RETURN='return'
        SET='set'
        STRING='string'
        TAKES='takes'
        THEN='then'
        TRUE='true'
        TYPE='type'

        COMMA=','
        EQ_EQ='=='
        EQ='='
        NEQ='!='
        LT_EQ='<='
        LT='<'
        GT_EQ='>='
        GT='>'
        PLUS='+'
        MINUS='-'
        MUL='*'
        DIV='/'
        LPAREN='('
        RPAREN=')'
        LBRACK='['
        RBRACK=']'

        REALVAl='regexp:\d+\.\d*|\.\d+'
        HEXVAL='regexp:(0x|\$)[0-9a-fA-F]+'
        INTVAL='regexp:\d+'
        RAWVAl="regexp:'[^']*'"
        STRVAL="regexp:\"([^\"\\]|\\.)*\""

        ID='regexp:[A-Za-z_][_0-9A-Za-z]*'
    ]
}

Root ::= (
    TypeDef |
    NativeDef |
    Glob |
    FuncDef
    )* {recoverWhile=rootRecover}
private rootRecover ::= !(TYPE|CONSTANT|GLOBALS|NATIVE|FUNCTION)

// === globals
Glob ::= GLOBALS Gvar* ENDGLOBALS {pin=1}
GvarName ::= ID
Gvar ::= CONSTANT? TypeName ARRAY? GvarName [EQ Expr] {pin=2 recoverWhile=GvarRecover}
private GvarRecover ::= !(CONSTANT|GLOBALS|ENDGLOBALS|TypeName)

// === TYPE
TypeDef ::= TYPE TypeName EXTENDS TypeNameBase {pin=1}
TypeName ::= ID|HANDLE|INTEGER|REAL|BOOLEAN|STRING|CODE
TypeNameBase ::= TypeName

// === VARIABLE
TypedVar ::= TypeName ID
TypedVarList ::= TypedVar (COMMA TypedVar)* {pin=1 recoverWhile=TypedVarListRecover}
private TypedVarListRecover ::= !(RETURNS)

// === FUNCTION
Arg ::= Expr|FuncAsCode
ArgList ::= Arg (COMMA Arg)*
FuncCall ::= FuncCallName LPAREN ArgList? RPAREN {pin=2}
FuncCallName ::= ID
FuncDefName ::= ID
FuncTakes ::= TAKES (NOTHING|TypedVarList)
FuncReturns ::= RETURNS (NOTHING|TypeName)
NativeDef ::= CONSTANT? NATIVE FuncDefName FuncTakes? FuncReturns? {pin=2}

FuncDef ::= CONSTANT? FUNCTION FuncDefName FuncTakes? FuncReturns? Stmt* ENDFUNCTION {pin=2}

// === STATEMENT
Stmt ::=
    SetStmt |
    CallStmt |
    LocalVarStmt |
    ReturnStmt |
    IfStmt |
    LoopStmt |
    ExitWhenStmt

LocalVarStmt ::= LOCAL? TypeName ARRAY? ID (EQ Expr)? {pin=2}

SetStmt ::= SET? (ArrayAccess|ID) EQ Expr {pin=3}

CallStmt ::= DEBUG? CALL? FuncCall

ReturnStmt ::= RETURN Expr? {pin=1}

IfStmt ::= IF Expr THEN? (Stmt|ElseIfStmt|ElseStmt)* ENDIF {pin=1}
ElseIfStmt ::= ELSEIF Expr THEN? Stmt* {pin=1}
ElseStmt ::= ELSE Stmt* {pin=1}

LoopStmt ::= LOOP Stmt* ENDLOOP {pin=1}
ExitWhenStmt ::=EXITWHEN Expr {pin=1}

// === EXPRESSION
// https://github.com/JetBrains/Grammar-Kit/blob/master/TUTORIAL.md
// https://github.com/JetBrains/Grammar-Kit/blob/master/testData/generator/ExprParser.bnf
// https://intellij-support.jetbrains.com/hc/en-us/community/posts/115000094104-Expression-handling-in-grammar-kit-issue-with-deep-trees
// https://gist.github.com/FatalCatharsis/aad0b72c3ffed144c3d61ee73dcb283f

Expr ::=
    // plus
    PlusExpr |
    MinusExpr |
    // mul
    MulExpr |
    DivExpr |
    // unary
    MulUnaryExpr |
    DivUnaryExpr |
    PlusUnaryExpr |
    MinusUnaryExpr |
    NotExpr |
    // compare
    EqExpr |
    NeqExpr |
    LtExpr |
    LtEqExpr |
    GtExpr |
    GtEqExpr |
    // bool
    OrExpr |
    AndExpr |
    // primary
    PrimExpr

PlusExpr ::= Expr PLUS Expr
MinusExpr ::= Expr MINUS Expr

MulExpr ::= Expr MUL Expr
DivExpr ::= Expr DIV Expr

MulUnaryExpr ::= MUL Expr
DivUnaryExpr ::= DIV Expr
PlusUnaryExpr ::= PLUS Expr
MinusUnaryExpr ::= MINUS Expr
NotExpr ::= NOT Expr

EqExpr ::= Expr EQ_EQ Expr
NeqExpr ::= Expr NEQ Expr
LtExpr ::= Expr LT Expr
LtEqExpr ::= Expr LT_EQ Expr
GtExpr ::= Expr GT Expr
GtEqExpr ::= Expr GT_EQ Expr

OrExpr ::= Expr OR Expr
AndExpr ::= Expr AND Expr
ParenExpr ::= LPAREN Expr RPAREN


PrimVal ::= FALSE|NULL|TRUE

PrimExpr ::=
    PrimVal |
    ParenExpr |
    ArrayAccess |
    FuncCall |
    FuncAsCode |
    HEXVAL |
    REALVAL |
    INTVAL |
    RAWVAL |
    STRVAL |
    ID

ArrayAccess ::= ID LBRACK Expr? RBRACK
FuncAsCode ::= FUNCTION FuncCallName {pin=1}
