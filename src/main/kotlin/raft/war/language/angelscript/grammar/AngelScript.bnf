{
    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="AngelScript"
    psiImplClassSuffix="Impl"

    parserClass=              "raft.war.language.angelscript.grammar.AngelScriptParser"
    psiPackage=               "raft.war.language.angelscript.psi"
    psiImplPackage=           "raft.war.language.angelscript.psi.impl"
    elementTypeHolderClass=   "raft.war.language.angelscript.psi.AngelScriptTypes"
    elementTypeClass=         "raft.war.language.angelscript.psi.tree.AngelScriptIElement"
    tokenTypeClass=           "raft.war.language.angelscript.psi.tree.AngelScriptIToken"

    generateTokenAccessors=true

    extends(".*Expr")=Expr

    tokens=[
        WHITE_SPACE='regexp:\s+'
        LINE_COMMENT="regexp://[^\n]*"
        BLOCK_COMMENT="regexp:/\*([^\n])*\*/"
        ABSTRACT='abstract'
        ARRAY='array'
        AND='and'
        AUTO='auto'
        BOOL='bool'
        BREAK='break'
        CASE='case'
        CAST='cast'
        CODE='code'
        CONST='const'
        CLAS='class'
        DOUBLE='double'
        DEFAULT='default'
        DO='do'
        ELSE='else'
        ENUM='enum'
        EXTERNAL='external'
        EXPLICIT='explicit'
        FALSE='false'
        FINAL='final'
        FLOAT='float'
        FOR='for'
        FUNCDEF='funcdef'
        FUNCTION='function'
        GET='get'
        HANDLE='handle'
        IF='if'
        INCLUDE='include'
        IN = 'in'
        IS = 'is'
        INOUT='inout'
        INT = 'int'
        INT8 = 'int8'
        INT16 = 'int16'
        INT32 = 'int32'
        INT64 = 'int64'
        NAMESPACE='namespace'
        NOT='not'
        NULL='null'
        NIL='nil'
        OR='or'
        OVERRIDE='override'
        OUT='out'
        PRIVATE='private'
        PROPERTY='property'
        PROTECTED='protected'
        RETURN='return'
        SET='set'
        SHARED='shared'
        STATIC='static'
        STRING='string'
        SWITCH='switch'
        TRUE='true'
        UINT = 'uint'
        UINT8 = 'uint8'
        UINT16 = 'uint16'
        UINT32 = 'uint32'
        UINT64 = 'uint64'
        VOID = 'void'
        XOR='xor'
        WHILE='while'

        NIS='!is'
        AT='@'
        PERCENT='%'
        PERCENT_EQ='%='
        HASH='#'
        TILDE='~'
        DOT='.'
        COMMA=','
        EQ_EQ='=='
        EQ='='
        NEQ='!='
        COLON_COLON='::'
        COLON=':'
        SEMI = ';'
        MINUS_GT = '->'
        AMP_AMP = '&&'
        AMP_EQ='&='
        AMP='&'
        VBAR_VBAR = '||'
        VBAR_EQ='|='
        VBAR='|'
        EXCL='!'
        PLUS_PLUS='++'
        PLUS='+'
        PLUS_EQ='+='
        MINUS_MINUS='--'
        MINUS='-'
        MINUS_EQ='-='
        MUL_MUL_EQ='**='
        MUL_MUL='**'
        MUL='*'
        MUL_EQ='*='
        DIV='/'
        DIV_EQ='/='
        LT_EQ='<='
        LT_LT_EQ='<<='
        LT_LT='<<'
        LT='<'
        GT_EQ='>='
        GT_GT_GT_EQ='>>>='
        GT_GT_GT='>>>'
        GT_GT_EQ='>>='
        GT_GT='>>'
        GT='>'
        QUEST='?'
        CAR_CAR='^^'
        CAR_EQ='^='
        CAR='^'
        LBRACE = '{'
        RBRACE = '}'
        LPAREN = '('
        RPAREN = ')'
        LBRACK = '['
        RBRACK = ']'

        RAWVAL="regexp:'[^']*'"
        REALVAL='regexp:(\d+\.\d*|\.\d+)([fd])?'
        HEXVAL='regexp:(0x|\$)[0-9a-fA-F]+'
        INTVAL='regexp:\d+'

        // https://stackoverflow.com/questions/46133654/antlr-grammar-for-triple-quoted-string
        // https://github.com/JetBrains/intellij-community/blob/master/python/python-parser/src/com/jetbrains/python/lexer/Python.flex
        STRING_ONE='regexp:"([^"\\]|\\.)*"'
        STRING_THREE='regexp:\"\"\"(.*?)\"\"\"'
        // https://github.com/hmatt1/angelscript-intellij/blob/main/src/main/java/org/intellij/sdk/language/angelscript.bnf

        ID='regexp:[A-Za-z_][_0-9A-Za-z]*'
    ]

}
// https://www.angelcode.com/angelscript/sdk/docs/manual/doc_script_bnf.html

Root ::= (IncludeStmt|Enums|Var|Fun|FunDef|Clazz|Nspace|SEMI)* {recoverWhile=RootRecover}

private RootRecover ::= !(PrimType|QUEST|AUTO|CLAS|HASH|NAMESPACE|ENUM|CONST)


//ENUM ::=  {'shared' | 'external'}
//          'enum'
//          IDENTIFIER  (
//                          ';' |   (
//                                      '{'
//                                          IDENTIFIER ['=' EXPR] {',' IDENTIFIER ['=' EXPR]}
//                                      '}'
//                                  )
//                      )
Enums ::= (SHARED|EXTERNAL)? ENUM ID (SEMI | EnumStatBlock)
EnumStatBlock ::= LBRACE EnumItem? (COMMA EnumItem)* COMMA* RBRACE
EnumItem ::= ID (EQ Expr)?

//NAMESPACE     ::= 'namespace' IDENTIFIER {'::' IDENTIFIER} '{' SCRIPT '}'
Nspace ::= NAMESPACE ID (COLON_COLON ID)* NspaceStatBlock {pin=1}
NspaceStatBlock ::= LBRACE NspaceItems RBRACE
private NspaceItems ::= (IncludeStmt|Enums|Var|Fun|FunDef|Clazz|Nspace|SEMI)* //{recoverWhile=StatBlockRecover}

IncludeStmt ::= HASH? INCLUDE Str {pin=2}

// FUNCDEF       ::= {'external' | 'shared'} 'funcdef' TYPE ['&'] IDENTIFIER PARAMLIST ';'
// CLASS         ::= {'shared' | 'abstract' | 'final' | 'external'} 'class' IDENTIFIER  (
//                                                                                          ';'
//                                                                                          |
//                                                                                          (
//                                                                                              [':' IDENTIFIER {',' IDENTIFIER}]
//                                                                                              '{'
//                                                                                              {VIRTPROP | FUNC | VAR | FUNCDEF}
//                                                                                              '}'
//                                                                                          )
//                                                                                      )
Clazz ::= (SHARED|ABSTRACT|FINAL|EXTERNAL)* CLAS ID (SEMI | ((COLON ID (COMMA ID)*)? ClazzStatBlock)) {pin=2}
ClazzItem ::= (VirtProp|Constructor|Fun|Var|FunDef)*  {recoverWhile=ClazzItemRecover}
private ClazzItemRecover ::= !(SEMI|ID|RBRACE|Type)
ClazzStatBlock ::= LBRACE ClazzItem RBRACE
Constructor ::= ID ParamList CONST? FuncAttr (SEMI | StatBlock)

// VIRTPROP      ::= ['private' | 'protected'] TYPE ['&'] IDENTIFIER    '{'
//                                                                      {
//                                                                          ('get' | 'set') ['const'] FUNCATTR (STATBLOCK | ';')
//                                                                      }
//                                                                      '}'
VirtProp ::= (PRIVATE|PROTECTED)? Type AMP? ID LBRACE ((GET|SET) CONST? FuncAttr (StatBlock | SEMI))*
FunDef ::= (EXTERNAL|SHARED)* FUNCDEF Type AMP? ID ParamList SEMI
// VAR ::=  ['private'|'protected']
//          TYPE
//          IDENTIFIER
//          [
//              ('=' (INITLIST | EXPR)) | ARGLIST
//          ]
//          {
//              ','
//              IDENTIFIER
//              [
//                  ('=' (INITLIST | EXPR)) | ARGLIST
//              ]
//          }
//          ';'
Var ::= (PRIVATE|PROTECTED)?
        Type
        ID
        (
            (EQ (InitList | Expr)) | ArgList
        )?
        (
            COMMA
            ID
            (
                (EQ (InitList|Expr)) | ArgList
            )?
        )*
        SEMI

// TYPE ::=
//          ['const']
//          SCOPE
//          DATATYPE
//          [
//              '<' TYPE {',' TYPE} '>'
//          ]
//          {
//              ('[' ']') | ('@' ['const'])
//          }
Type ::=    CONST?
            Scope
            DataType
            GenericType?
            (
                (LBRACK RBRACK)|(AT CONST?)
            )*
GenericType ::= LT Type (COMMA Type)* GT
// SCOPE         ::= ['::'] {IDENTIFIER '::'}   [
//                                                  IDENTIFIER
//                                                  [
//                                                      '<' TYPE {',' TYPE} '>'
//                                                  ]
//                                                  '::'
//                                              ]
Scope ::= COLON_COLON? (ID COLON_COLON)* (ID GenericType? COLON_COLON)?
// DATATYPE      ::= (IDENTIFIER | PRIMTYPE | '?' | 'auto')
DataType ::= ID|PrimType|QUEST|AUTO
// PRIMTYPE      ::= 'void' | 'int' | 'int8' | 'int16' | 'int32' | 'int64' | 'uint' | 'uint8' | 'uint16' | 'uint32' | 'uint64' | 'float' | 'double' | 'bool'
PrimType ::= ARRAY|BOOL|CODE|DOUBLE|FLOAT|HANDLE|INT|INT8|INT16|INT32|INT64|STRING|UINT|UINT16|UINT32|UINT64|VOID|ID
// INITLIST      ::=    '{'
//                      [ASSIGN | INITLIST]
//                      {
//                          ','
//                          [ASSIGN | INITLIST]
//                      }
//                      '}'
InitList ::=    LBRACE
                (Assign|InitList)?
                (
                    COMMA
                    (Assign|InitList)?
                )*
                RBRACE
// ASSIGN        ::= CONDITION [ASSIGNOP ASSIGN]
Assign ::= Condition (AssignOp Assign)?
// CONDITION     ::= EXPR ['?' ASSIGN ':' ASSIGN]
Condition ::= Expr (QUEST Assign COLON Assign)?
// ARGLIST       ::=    '('
//                      [IDENTIFIER ':']
//                      ASSIGN
//                      {
//                          ',' [IDENTIFIER ':'] ASSIGN
//                      }
//                      ')'
ArgList ::= LPAREN (ID COLON)? Assign (COMMA (ID COLON)? Assign)* RPAREN
// FUNCCALL      ::= SCOPE IDENTIFIER ARGLIST
FuncCall ::= Scope ID (ArgList | (LPAREN RPAREN))
// FUNC          ::= {'shared' | 'external'} ['private' | 'protected']  [
//                                                                          ( (TYPE ['&']) | '~' )
//                                                                      ]
//                                                                      IDENTIFIER PARAMLIST ['const'] FUNCATTR (';' | STATBLOCK)
private FunType ::= Type AMP? AT?
Fun ::= (SHARED|EXTERNAL)* (PRIVATE|PROTECTED)? (FunType|TILDE)? ID ParamList CONST? FuncAttr (SEMI | StatBlock)
// PARAMLIST     ::=    '('
//                      [
//                          'void'
//                          |
//                          (
//                              TYPE TYPEMOD [IDENTIFIER] ['=' EXPR]    {
//                                                                          ',' TYPE TYPEMOD [IDENTIFIER] ['=' EXPR]
//                                                                      }
//                          )
//                      ]
//                      ')'
ParamList ::= LPAREN (VOID | (Param (COMMA Param)*) )? RPAREN
Param ::= Type TypeMod? ID? (EQ Expr)? {recoverWhile=ParamRecover}
private ParamRecover ::= !(RPAREN|COMMA);

// TYPEMOD       ::= ['&' ['in' | 'out' | 'inout']]
TypeMod ::= AMP (IN|OUT|INOUT)?
// FUNCATTR      ::= {'override' | 'final' | 'explicit' | 'property'}
FuncAttr ::= (OVERRIDE|FINAL|EXPLICIT|PROPERTY)*
// STATBLOCK     ::= '{' {VAR | STATEMENT} '}'
StatBlock ::= LBRACE (Var | Stmt)* RBRACE
// STATEMENT     ::= (IF | FOR | WHILE | RETURN | STATBLOCK | BREAK | CONTINUE | DOWHILE | SWITCH | EXPRSTAT | TRY)
private Stmt ::= (StatBlock|ExprStat|IfStmt|ForStmt|WhileStmt|DoWhileStmt|SwitchStmt|BreakStmt|ReturnStmt)
// EXPRSTAT      ::= [ASSIGN] ';'
ExprStat ::= Assign? SEMI
// IF            ::= 'if' '(' ASSIGN ')' STATEMENT ['else' STATEMENT]
IfStmt ::= IF LPAREN Assign RPAREN Stmt ElseStmt? {pin=1}
ElseStmt ::= ELSE Stmt {pin=1}
// FOR           ::= 'for' '(' (VAR | EXPRSTAT) EXPRSTAT    [
//                                                              ASSIGN {',' ASSIGN}
//                                                          ]
//                                                          ')'
//                                                          STATEMENT
ForStmt ::=  FOR LPAREN (Var | ExprStat) ExprStat (Assign (COMMA Assign)*)? RPAREN Stmt
// WHILE         ::= 'while' '(' ASSIGN ')' STATEMENT
WhileStmt ::= WHILE LPAREN Assign RPAREN Stmt
// DOWHILE       ::= 'do' STATEMENT 'while' '(' ASSIGN ')' ';'
DoWhileStmt ::= DO Stmt WHILE LPAREN Assign RPAREN SEMI
// RETURN        ::= 'return' [ASSIGN] ';'
ReturnStmt ::= RETURN Assign? SEMI
// SWITCH        ::= 'switch' '(' ASSIGN ')' '{' {CASE} '}'
SwitchStmt ::= SWITCH LPAREN Assign RPAREN SwitchStatBlock
SwitchStatBlock ::= LBRACE (CaseStmt)* RBRACE
// CASE          ::= (('case' EXPR) | 'default') ':' {STATEMENT}
CaseStmt ::= ((CASE Expr) | DEFAULT) COLON CaseStmtList
CaseStmtList ::= Stmt*
// BREAK         ::= 'break' ';'
BreakStmt ::= BREAK SEMI
// LAMBDA ::=   'function'
//              '('
//              [
//                  [TYPE TYPEMOD]
//                  [IDENTIFIER]
//                  {
//                      ','
//                      [TYPE TYPEMOD]
//                      [IDENTIFIER]
//                  }
//              ]
//              ')'
//              STATBLOCK
Lambda ::= FUNCTION LPAREN ((Type TypeMod)? ID? (COMMA (Type TypeMod)? ID?)*)? RPAREN StatBlock

// === EXPRESSION
// https://github.com/JetBrains/Grammar-Kit/blob/master/TUTORIAL.md
// https://github.com/JetBrains/Grammar-Kit/blob/master/testData/generator/ExprParser.bnf
// https://intellij-support.jetbrains.com/hc/en-us/community/posts/115000094104-Expression-handling-in-grammar-kit-issue-with-deep-trees
// https://gist.github.com/FatalCatharsis/aad0b72c3ffed144c3d61ee73dcb283f
// https://learn.microsoft.com/en-us/cpp/cpp/cpp-built-in-operators-precedence-and-associativity?view=msvc-170

Expr ::=
//  15
    TernarExpr | // Правая
    AssignExpr| // Правая
//  14
    OrExpr| // 14 | Левая | || | Логическое ИЛИ | a||b
//  13
    AndExpr| // 13 | Левая | && | Логическое И | a&&b
//  12
    BOrExpr| // 12 | Левая | | | Битовое ИЛИ | a|b
//  11
    BXorExpr| // 11 | Левая | ^ | Битовое исключающее ИЛИ | a^b
//  10
    BAndExpr| // 10 | Левая | & | Битовое И | a&b
//  9
    EqExpr| // 9 | Левая | == | Равно | a==b
    NEqExpr| // 9 | Левая | != | Не равно | a!=b
    XorExpr| // 9 | Левая | ^^ | Логическое исключающее ИЛИ | a^^b
    IsExpr| // 9 | Левая | is | Принадлежит | a is b
//  8
    LTExpr| // 8 | Левая | < | Меньше | a<b
    GTExpr| // 8 | Левая | > | Больше | a>b
    LTEqExpr| // 8 | Левая | <= | Меньше или равно | a<=b
    GTEqExpr| // 8 | Левая | >= | Больше или равно | a>=b
//  7
    BShiftLExpr| // 7 | Левая | << | Битовый сдвиг влево | a<<b
    BShiftRExpr| // 7 | Левая | >> | Битовый сдвиг вправо | a>>b
    BShiftRAExpr| // 7 | Левая | >>> | Битовый сдвиг вправо c сохранением знакового бита | a>>>b
//  6
    PlusExpr| // 6 | Левая | + | Сложение | a+b
    MinusExpr| // 6 | Левая | - | Вычитание | a-b
//  5
    MulExpr| // 5 | Левая | * | Умножение | a*b
    DivExpr| // 5 | Левая | / | Деление | a/b
    ModExpr| // 5 | Левая | % | Модуль (оставшаяся часть от деления) | a%b
//  4
    PowExpr| // 4 | Левая | ** | Возведение в степень | a**b
//  3
    PreIncExpr| // 3 | Правая | ++ | Преинкримент | ++a
    PreDecExpr| // 3 | Правая | ++ | Преинкримент | ++a
    BitNotUnExpr| // 3 | Правая | {{~}} | Побитовое отрицание | {{~}}a
    NotExpr| // 3 | Правая | ! | Логическое НЕ | !a
    MinusUnExpr| // 3 | Правая | - | Математическая смена знака | -a
    PlusUnExpr| // 3 | Правая | + | Математическое взятие модуля | +a
    // 3 | Правая | @ | Ссылка | @a
    // 3 | Правая | () | Приведение типа | a(b)
    // 3 | Правая | cast | Приведение типа | cast<a>(b)
    MulUnExpr| // Added for inspection
    DivUnExpr| // Added for inspection
//  2
    RefExpr| // 2 | Левая | . | Доступ к члену | a.b
    // 2 | Левая | [] | Доступ к индексу | a[b]
    // 2 | Левая | () | Вызов функции | a(b)
    PostIncExpr| // 2 | Левая | ++ | Постинкримент | a++
    PostDecExpr| // 2 | Левая | -- | Постдекримент | a--
//  1
    ScopeExpr| // 1 | Нет | :: | Область видимости | a::b
//  0 Primary
    PrimExpr

// ASSIGNOP      ::= '=' | '+=' | '-=' | '*=' | '/=' | '|=' | '&=' | '^=' | '%=' | '**=' | '<<=' | '>>=' | '>>>='
AssignOp ::=
        VBAR_EQ| // 15 | Правая | |= | Присваивающее битовое ИЛИ | a|=b
        CAR_EQ| // 15 | Правая | ^= | Присваивающее битовое исключающее ИЛИ | a^=b
        AMP_EQ| // 15 | Правая | &= | Присваивающее битовое И | a&=b
        GT_GT_GT_EQ|// 15 | Правая | >>>= | Присваивающий битовый сдвиг вправо c сохранением знакового бита | a>>>=b
        GT_GT_EQ| // 15 | Правая | >>= | Присваивающий битовый сдвиг вправо | a>>=b
        LT_LT_EQ| // 15 | Правая | <<= | Присваивающий битовый сдвиг влево | a<<=b
        PERCENT_EQ| // 15 | Правая | %= | Присваивающее деление с остатком | a%=b
        MUL_MUL_EQ| // 15 | Правая | **= | Присваивающее возведение в степень | a**=b
        EQ| // 15 | Правая | = | Присваивание | a=b
        PLUS_EQ| // 15 | Правая | += | Присваивающее сложение | a+=b
        MINUS_EQ| // 15 | Правая | -= | Присваивающее вычитание | a-=b
        MUL_EQ| // 15 | Правая | *= | Присваивающее умножение | a*=b
        DIV_EQ // 15 | Правая | /= | Присваивающее деление | a/=b

AssignExpr ::= Expr AssignOp Expr

RefExpr ::= Expr DOT Expr
ScopeExpr ::= Expr COLON_COLON Expr

PlusExpr ::= Expr PLUS Expr
MinusExpr ::= Expr MINUS Expr
MulExpr ::= Expr MUL Expr
DivExpr ::= Expr DIV Expr

PowExpr ::= Expr MUL_MUL Expr
ModExpr ::= Expr PERCENT Expr

IsExpr ::= Expr (IS|NIS) Expr
BitNotUnExpr ::= TILDE Expr

BShiftLExpr ::= Expr LT_LT Expr
BShiftRExpr ::= Expr GT_GT Expr
BShiftRAExpr ::= Expr GT_GT_GT Expr

XorExpr ::= Expr (XOR|CAR_CAR) Expr
BXorExpr ::= Expr CAR Expr
BOrExpr ::= Expr VBAR Expr

BAndExpr ::= Expr AMP Expr

MulUnExpr ::= MUL Expr
DivUnExpr ::= DIV Expr
PlusUnExpr ::= PLUS Expr
MinusUnExpr ::= MINUS Expr
NotExpr ::= (NOT|EXCL) Expr
PreIncExpr ::= PLUS_PLUS Expr
PostIncExpr ::= Expr PLUS_PLUS
PreDecExpr ::= MINUS_MINUS Expr
PostDecExpr ::= Expr MINUS_MINUS

EqExpr ::= Expr EQ_EQ Expr
NEqExpr ::= Expr NEQ Expr
LTExpr ::= Expr LT Expr
LTEqExpr ::= Expr LT_EQ Expr
GTExpr ::= Expr GT Expr
GTEqExpr ::= Expr GT_EQ Expr

OrExpr ::= Expr (OR|VBAR_VBAR) Expr
AndExpr ::= Expr (AND|AMP_AMP) Expr
ParenExpr ::= LPAREN Expr RPAREN

TernarExpr ::= Expr QUEST Expr COLON Expr

PrimExpr ::=
    TRUE |
    FALSE |
    NULL |
    NIL |
    ParenExpr |
    ArrayAccess |
    FuncCall |
    Lambda |
    RAWVAL |
    REALVAL |
    HEXVAL |
    INTVAL |
    Str |
    (AT? ID)


Str ::= (STRING_ONE|STRING_THREE)+
ArrayAccess ::= ID ArrayAccessItem ArrayAccessItem*
ArrayAccessItem ::= LBRACK Expr? RBRACK {pin=1}
